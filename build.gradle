buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
	springFoxVersion = '2.8.0'
}

dependencyManagement {
	dependencies {
		dependency "io.springfox:springfox-swagger2:${springFoxVersion}"
		dependency "io.springfox:springfox-swagger-ui:${springFoxVersion}"

	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compileOnly('org.projectlombok:lombok')

	// swagger
	compile('io.springfox:springfox-swagger2')
	compile('io.springfox:springfox-swagger-ui')

	// database
	compile('mysql:mysql-connector-java')
	compile('javax.xml.bind:jaxb-api')
	compile('org.liquibase:liquibase-core')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('com.h2database:h2')
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

idea {
	module {
		testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
		testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
	}
}

task buildDocker(type: Exec) {
	doFirst {
		copy {
			from "src/main/docker/"
			into "build/docker/"
			include "*"
		}
		copy {
			from "build/libs"
			into "build/docker/"
			include "*.jar*"
		}
	}
	commandLine 'docker', 'image', 'build', '-f', 'build/docker/Dockerfile', '-t', project.name, 'build/docker/'
}

buildDocker.dependsOn build
